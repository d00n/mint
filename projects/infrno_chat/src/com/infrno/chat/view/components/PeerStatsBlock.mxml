<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
				 xmlns:s="library://ns.adobe.com/flex/spark" 
				 xmlns:mx="library://ns.adobe.com/flex/mx"
				 creationComplete="handleCreationComplete(event);"
				 
				 >
	<fx:Script>
		<![CDATA[			
			import com.infrno.chat.model.vo.StatsVO;
			
			import mx.collections.ArrayCollection;
			
			public var peer_suid:String;		
			
			private var _peerStatsVO:StatsVO;
			
			[Bindable]
			public var user_name_label:String;
			
			[Bindable]
			public var droppedFrames_label:String;
			
			private var peerStatBlockConfig_AC:ArrayCollection = new ArrayCollection();

			private function initConfig():void{
				peerStatBlockConfig_AC.addItem( {yFieldName:"srtt", labelPrefix:"SRTT", toolTip:"Specifies the Smooth Round Trip Time for the NetStream session. This value returns a valid value only for RTMFP streams and returns 0 for RTMP streams."} );
				peerStatBlockConfig_AC.addItem( {yFieldName:"currentBytesPerSecond", labelPrefix:"Total", toolTip:"Specifies the rate at which the NetStream buffer is filled in bytes per second. The value is calculated as a smooth average for the total data received in the last second."} );
				peerStatBlockConfig_AC.addItem( {yFieldName:"audioBytesPerSecond", labelPrefix:"Audio", toolTip:"Specifies the rate at which the NetStream audio buffer is filled in bytes per second. The value is calculated as a smooth average for the audio data received in the last second."} );
				peerStatBlockConfig_AC.addItem( {yFieldName:"videoBytesPerSecond", labelPrefix:"Video", toolTip:"Specifies the rate at which the NetStream video buffer is filled in bytes per second. The value is calculated as a smooth average for the video data received in the last second."} );
				peerStatBlockConfig_AC.addItem( {yFieldName:"dataBytesPerSecond", labelPrefix:"Data", toolTip:"Specifies the rate at which the NetStream data buffer is filled in bytes per second. The value is calculated as a smooth average for the data messages received in the last second."} );
				peerStatBlockConfig_AC.addItem( {yFieldName:"audioLossRate", labelPrefix:"Audio loss rate", toolTip:"Specifies the audio loss for the NetStream session. This value returns a valid value only for RTMFP streams and would return 0 for RTMP streams. Loss rate is defined as the ratio of lost messages to total messages."} );
				// Hack hack hack - The last item added doesn't get it's label displayed. So just tack an empty one on.
				peerStatBlockConfig_AC.addItem( {yFieldName:"", labelPrefix:""} );
				
			}
			
			
			public function set client_peerStatsVO(value:StatsVO):void{
				_peerStatsVO = value;
				var sparkline:Sparkline;
				for (var i:int=0; i<peerSparkline_vgroup.numChildren-1; i++){
					sparkline = peerSparkline_vgroup.getChildAt(i) as Sparkline;
					sparkline.statsVO = _peerStatsVO;
					sparkline.lastValue_label = sparkline.lastValuePrefix +": "+ _peerStatsVO.lastDataRecord[sparkline.yFieldName];
				}				
			}

			
			// TODO What is best place/time to do this?
			private function handleCreationComplete(event:Event):void	{	
				initConfig();
				createPeerBlock();
			}
			
			public function createPeerBlock():void{
				var yFieldName:String;
				var labelPrefix:String;
				var toolTip:String;
				var sparklineConfig:Object;
				for (var i:int = 0; i < peerStatBlockConfig_AC.length; i++) {
					sparklineConfig = peerStatBlockConfig_AC.getItemAt(i);
					yFieldName 			= sparklineConfig.yFieldName;
					labelPrefix 		= sparklineConfig.labelPrefix;
					toolTip 				= sparklineConfig.toolTip;
					createSparkline(yFieldName, labelPrefix, toolTip);
				}
			}

			
			// WARNING: instantiating Sparkline in the mediator causes the backward/outside container display bug
			public function createSparkline(yFieldName:String, labelPrefix:String="", toolTip:String=""):void {
				var _sparkline:Sparkline 		= new Sparkline(); 
				_sparkline.yFieldName 			= yFieldName;
				_sparkline.lastValuePrefix 	= labelPrefix;
				_sparkline.toolTip			 		= toolTip;
				_sparkline.width 						= borderContainer.width;
				
				peerSparkline_vgroup.addElement(_sparkline);	
			}				
			
			
		]]>
	</fx:Script>	
	<s:BorderContainer id="borderContainer"
										 backgroundColor="#cecece" 
										 borderColor="#cecece"
										 width="158" 
										 >
		
		<s:Rect id="background" 
						width="160" 
						height="100%">
			<s:fill>
				<s:SolidColor color="#FFFFFF"/>
			</s:fill>
		</s:Rect>
				
		<s:VGroup id="peerBlock_vgroup">			
			<s:Label text="{user_name_label}"
							 paddingTop="3"
							 paddingLeft="3"
							 paddingRight="3"
							 backgroundColor="0xFFFFFF" />		
			
			<s:Label text="{droppedFrames_label}"
							 paddingLeft="3"
							 paddingRight="3"
							 backgroundColor="0xFFFFFF" />
			
			<s:VGroup id="peerSparkline_vgroup">			
			</s:VGroup>
		</s:VGroup>
			
	</s:BorderContainer>	
</s:Group>
